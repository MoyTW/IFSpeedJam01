*comment ########################################
*comment # WORK ON TARGET 4                     #
*comment ########################################
*label work_on_target_4
*gosub sub_discuss_building_4
*goto lobby_lab_d

*comment ########################################
*comment # DISCUSSING THE BUILDING              #
*comment ########################################
*label sub_discuss_building_4
*temp at_lab_complex false
*temp target_lab ""
*temp times_proposed_wrong 0
*temp discussed_already false

"This one's one of the three labs," you say. You check your map. "The labs are labeled A, B, and C, and the clue is just three names. Adams, Douglas, Bowie, David, and Darwin, Charles."

"Wait," says Carlos, "so the clues are Douglas Adams, David Bowie, and Charles Darwin?"

"Yeah, but they're written last name first on the sheet. I think this one's a logic puzzle, except that I don't know who Douglas Adams is."

"He's a British comedy writer. He wrote The Hitchiker's Guide to the Galaxy, the Dirk Gently series, and also wrote some Doctor Who."

*fake_choice
  #Gremlin: "Is Dirk Gently some sort of old-timey English euphemism for fucking?"
    Carlos opens his mouth, then closes it, then opens it, then closes it. "Uhm." He frowns. "I don't…I don't think so, but don't quote me on that." He stares off into the distance. "I haven't actually read them, so if there's some sort of wordplay there I wouldn't know it."
    
    "There is [i]no way[/i] that it's not some sort of joke," you say. "I mean, listen." You put on your best English Gentleman voice, which is very definitely not good in any way shape or form, and say to Carlos, "I'll dirk [i]you[/i] gently."
    
    Mouse collapses in laughter and Carlos sighs. "Wow, ok, that, uh, that, uh. Let's get back to the hunt now, shall we?"
    
    "Sure," you say. "I just want you to remember that you were the one who said the words 'Dirk Gently' first."
    
    He sighs. "Douglas Adams, not me."
  #Considerate: "Thanks for explaining. Have you read his work?"
    "I've read The Hitchiker's Guide to the Galaxy series," he says, "which is really good."
    
    "Yeah, I've heard of it. It's pretty popular, I think."
    
    "Yeah, it's very funny. The earlier ones are funny funny, but the later ones get kind of depressing funny."
    
    You nod along. "How do you think that relates to Darwin and Bowie?"
    
    He shrugs. "Let me think on that," he says.
  #Nonplussed: "So, basically, he's a British comedy author, paired with a singer and actor and the scientist who discovered evolution."
    Carlos nods. "Yeah," he says, "that seems right. I'm not sure how they relate to the lab buildings, but that seems right."

Mouse leans over to you. "Can I see the sheet?" she whispers. You hand it to her, she looks at it for a few moments, then gives it back to you.

You put on an encouraging smile. "Do you have a guess?" you ask. She nods. How do you do it, Mouse? Tell me your secrets! Educate me, wise but silent master! "I'm not sure though," she adds.

You stare at the sheet in silence as Carlos pulls out his phone. Is that…tapping you hear? It's quiet, but that's definitely some sort of tapping. You glance over to see Mouse's foot bouncing up and down, her eyes distant.

You look back at the clue.

• The target is in one of the three labs.
*line_break
• Adams, Douglas
*line_break
• Bowie, David
*line_break
• Darwin, Charles

*label lab_choice
*choice
  *disable_reuse #"Let's try Lab A."
    *set target_lab "Lab A"
    *goto propose_wrong
  *disable_reuse #"Let's try Lab B."
    *set target_lab "Lab B"
    *goto propose_wrong
  #"Let's try Lab D."
    *goto go_to_lab_d
  *hide_reuse *selectable_if ((puzzle_share_immediately) or (puzzle_discuss_final_answer)) #"What do you guys think?"
    Carlos sighs.
    *gosub carlos_mouse_explanation_d_snippet

    *goto lab_choice

*label propose_wrong
*set times_proposed_wrong + 1
*if times_proposed_wrong = 1
  *goto propose_wrong_first_time
*elseif times_proposed_wrong = 2
  *goto propose_wrong_second_time

*comment ########## Propose Wrong First Time ##########

*label propose_wrong_first_time
*if discussed_already
  "Sure," says Carlos, "I was leaning towards D but I wasn't very confident in it." You look at Mouse, and she nods.

  "All right," you say, "let's do ${target_lab}."

  *gosub first_lab_wrong_snippet
  *goto lab_choice
*elseif puzzle_discuss_final_answer
  "You sure?" asks Carlos.

  "What are you thinking?" you ask.

  *gosub carlos_mouse_explanation_d_snippet
  *choice
    #"All right, that seems plausible. Let's do Lab D."
      *goto go_to_lab_d
    #"I still think it's Lab A."
      "Okay," says Carlos, "let's take a look."

      *gosub first_lab_wrong_snippet
      *goto lab_choice
*else
  *gosub first_lab_wrong_snippet
  *goto lab_choice

*label first_lab_wrong_snippet
*set hours_remaining - 0.5
*gosub enter_first_lab_snippet

The lobby has a bunch of display cases with various chemistry-related things, like beakers and models of molecules. None of this looks related to leaves, but of course, it wouldn't be obvious, would it?

"It's not this one," says Mouse.

"What?" asks Carlos. "How can you tell?"

"No unlocked cabinets," she says. You check the clue sheet. Oh, she's right, it can't be this one. Okay, where next, then?
*return

*comment ########## Propose Wrong Second Time ##########
*label propose_wrong_second_time
*set hours_remaining - 0.5
Carlos grins. "It's fifty-fifty!"

"It's only fifty-fifty if I'm picking randomly," you reply.

"Are you?"

*fake_choice
  #"No, I'm sure I have it."
    "That's a dangerous thing to say," he says. "I'm sure you were sure last time too, weren't you?"
    
    "I'm sure this time," you say, and grin.
  #"No, but I'm not sure at all."
    "Fair enough," he says.
  #"Yes."
    "So," he says, "it's fifty-fifty."

Unfortunately, it turns out that ${target_lab} is not the right lab. The displays are pretty impressive, with models of rockets and engines and some fancy telescopes, but there are no unlocked cabinets to be seen.

Well, at least now you know which building it is.    
*goto lab_choice

*comment ########## Shared Labels ##########

*label enter_first_lab_snippet
*set at_lab_complex true
You take a leisurely walk over to the labs, a collection of three one-story buildings with windows all around the sides. They would look very modern and sharp except for the fact that the windows are uniformly grimy and dirty. The grounds nearby are overgrown, and the concrete paths cracking. It's like a scene from a post-apocalyptic movie.
      
You enter ${target_lab} through the main doors, which are themselves window doors, and find yourself in a sunny, well-lighted lobby. Somehow the windows look clean from the inside and dirty from the outside. That's weird, isn't it? It's really weird. But then, everything about the exteriors of the buildings here is weird.
*return

*label carlos_mouse_explanation_d_snippet
"Well," he says, "between the three of them, only Charles Darwin is a scientist, but he wasn't a lab scientist. Or at least that's not what he's famous for. So maybe that's a weak association. But just looking at the names, if you count the first letters, you have one A, one B, one C, and three Ds, which also points to Darwin, but then what's up with the hanging C? There's no Lab C. But I can't really find anything else."

You look over to Mouse. "What do you think, Mouse?"

"Lab D," she says. "For Darwin. Same reasons. But I'm not sure either."
*return

*label go_to_lab_d
"Lab D it is," assents Carlos.

*if (at_lab_complex = false)
  *gosub enter_first_lab_snippet
*return

*comment ########################################
*comment # LOBBY LAB D                          #
*comment ########################################
*label lobby_lab_d
*temp target_display_case ""
The lobby of Lab D is pretty small, with a long bench along the window wall and a series of display cases opposite. All the display cases have cabinets under them, so it's not quite as easy as that. You sigh and start examining their contents.

The first display case is a long ant farm, swarming with industrious little dark brown ants. The sign on the display says that they're a colony of atta cephalotes, which live by farming fungus. Really? Ants have agriculture? That's pretty cool.

The second display case has a stuffed fox, whose face is forever locked in an expression of comical surprise. There's a little sticky note on the side. "Dear students," it says, "Please stop putting hats on the Minnie. Thank you." Whoever wrote this note is a terrible killjoy!

The third display case has in it a collection of beautiful butterflies on little pins, with orange and purple and blue wings. Some of them even have several colors.

The fourth display case is a large fish tank with a bunch of different kinds of fish. They're very pretty, you'll give them that, but identifying fish is not really your thing, and they don't have a plaque like they do in aquariums.

*if puzzle_share_immediately
  Mouse speaks. "It's the ants." She pauses. "They're atta cephalotes."
  
  Yes, it says that on the display. So what exactly does that explain, Mouse? You could stand to be a little more wordy. I won't bite, I promise.
  *choice
    #"All right, ants it is."
      *goto put_sticker_on_ants
    #"I believe you, but can you explain a little more, Mouse?"
      "Atta cephalotes are leaf-cutter ants," she says.
      
      Did you look that up while I was watching the fish, Mouse, or do you just…know that? I mean, it's not bad to know that. You might be huge into ants. That's fine! It's just, you got that one real fast.
      
      "All right," you say, "that seems pretty strong." You look over at Carlos.
      
      "Yeah," he says, checking his phone, "I think that's our answer. Says here on Wikipedia. 'Atta cephalotes is a species of leafcutter ant in the tribe Attini.'"
      
      "All right, I'm convinced," you say.
      *goto put_sticker_on_ants
    #"No, I think it's one of the other displays."
      Mouse pauses. "Okay," she says.
      
      No, Mouse, it's not okay! Okay is not the word you should be using right now! It should be something like, No, Morgan, I think it's ants because of whatever reasons I think it's the ants, but don't just agree because I said it! Clarify, or object, or, you know, something! If you don't stand up for yourself, Mouse, who will?
      
      Of course, you don't say any of that. Instead you try to put on a smooth face and say, "Mouse, why do you think it's the ants?"
      
      "Atta cephalotes are leaf-cutter ants," she says. She holds her phone out to you and you take it in hand. It's on the Wikipedia page for atta cephalotes and, yep, right there in the first sentence - 'Atta cephalotes is a species of leafcutter ant in the tribe Attini.'
          
      "I see," you say, and put on a grin. "Thanks for explaining, Mouse." You hand her phone back to her. "Okay, I'm convinced! Ants it is." Hopefully next time she'll feel more comfortable with speaking up.
      *goto put_sticker_on_ants
*else
  *choice
    #"I think it's the ant farm."
      *if puzzle_discuss_final_answer
        "Pretty fast, Morgan," Carlos says. "I'm not sure yet."
        
        You look over to Mouse. "It's the ants," she says.
        
        "If Mouse thinks it's the ants," says Carlos, "it must be the ants."
        
        You grin.
        *goto put_sticker_on_ants
      *else
        "Pretty fast, Morgan," Carlos says. "I'm not sure yet, but don't wait for me." Mouse shrugs when you look over at her.
        
        *goto put_sticker_on_ants
    #"I think it's the stuffed fox."
      *set target_display_case "stuffed fox"
      *goto not_immediate_wrong_case
    #"I think it's the butterflies."
      *set target_display_case "butterflies"
      *goto not_immediate_wrong_case
    #"I think it's the fish tank."
      *set target_display_case "fish tank"
      *goto not_immediate_wrong_case

*label not_immediate_wrong_case
*if puzzle_discuss_final_answer
  "Pretty fast, Morgan," Carlos says. "I'm not sure yet."

  You look over to Mouse. "It's the ants," she says.

  "If Mouse thinks it's the ants," says Carlos, "it must be the ants." He paused. "How are the ants related to leaves, though?"
  
  "They're leaf-cutter ants," she says. "Atta cephalotes."
  
  You quickly Google "atta cephalotes" and yep, right there in the first sentence in the Wikipedia page. "Atta cephalotes is a species of leafcutter ant in the tribe Attini." Yeah, that's hard to argue with.
  
  "Ants it is," you say.
  *goto put_sticker_on_ants
*else
  *set sticker_used_4 true
  *set sticker_placed_correctly_4 false
  "Pretty fast, Morgan," Carlos says. "I'm not sure yet, but don't wait for me." Mouse shrugs when you look over at her.
  
  You open the cabinet underneath the ${target_display_case} to reveal cleaning supplies. You press a sticker against the back of the door and then close it.
  
  "All right," you say, "the lab's done with, number four down."
  *goto_scene ch4_hunt work_on_a_clue

*label put_sticker_on_ants
*set sticker_used_4 true
*set sticker_placed_correctly_4 true
You open the cabinet underneath the ant farm to reveal some cleaning supplies. You press a sticker against the back of the door before closing it.

*goto_scene ch4_hunt correctly_placed_sticker