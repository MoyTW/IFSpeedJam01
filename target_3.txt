*comment ########################################
*comment # WORK ON TARGET 3                     #
*comment ########################################
*label work_on_target_3
*gosub sub_discuss_building_3
*gosub sub_searching_hall_the_apostle
*return

*comment ########################################
*comment # DISCUSSING THE BUILDING              #
*comment ########################################
*label sub_discuss_building_3
*temp target_hall ""
*temp times_proposed_wrong 0

You read the clue out loud. "Which dorm has the strongest association with Saul?"

"Saul?" asks Carlos. "Like, Bob Odenkirk?"

"Sorry, who?"

"The actor who pays Saul Goodman in Breaking Bad and Better Call Saul." Carlos grins. "Good shows, by the way. You ever seen them?"

*choice
  #Gremlin: "Isn't Breaking Bad really old? Do you only watch old shows and movies, Carlos, or have you watched anything made in the last ten years?"
    "Ah," says Carlos, "actually, Breaking Bad ended in 2013. Clearly you don't watch enough TV!"
    
    "Really? You memorize the year dates for the old shows you watch?"
    
    "You can't hold my amazing memory against me. And you keep calling it old. Morgan, Morgan, 8 years isn't that old. My grandfather is still going at strong 97! It only seems old because you're so young."
    
    You can see Mouse struggling to suppress a grin. "Gah," you say, "they're right, Boomer really is a state of mind."
    
    Mouse breaks out in a fit of laughter as Carlos places a hand on his heart. "I am wise beyond my years," he intones.
    
    "Okay but seriously," you say, "I don't think it's the guy from Breaking Bad."
    
    "Yeah," he says, "you're probably right."
    
    After her giggles subside,
    *goto mouse_unfolds_her_map
  #Considerate: "I haven't. I don't watch many of the older shows, but I hear that they're regarded highly."
    "They're very good," he says. Then he pauses. "That's probably not what they're going for in this scavenger hunt."
    
    You shrug. "Either way's possible, but I've never seen it. Does it seem like it'd be relevant to any of the dorms?"
    
    "No," he says, "not at all."
    
    *goto mouse_unfolds_her_map
  #Nonplussed: "No. Still, I don't think they would make a pop-culture reference to a show that's out of our age range."
    "You're probably right." He unfolds his map and scans the dorm names. "You're definitely right," he says.
    
    *goto mouse_unfolds_her_map
*label mouse_unfolds_her_map
Mouse unfolds her map, nods, and starts typing into her phone. Well, she's clearly got ideas. On the other hand, Carlos looks pretty stumped. You stare at your own map. Monty Hall, Basket Hall, Hall of the Americas, Hall the Apostleâ€¦who named these dorms? These can't be real names. Well, Monty Hall and Basket Hall could be, but the others?

*if puzzle_share_immediately
  "Hall the Apostle," declares Mouse.
  
  "Wait," you say, "are you talking about Paul the Apostle?"
  
  "He was also called Saul," she clarifies. "I looked it up."
  
  You look over at Carlos. He shrugs. "I don't even know who Paul the Apostle is. My parents weren't religious."
  
  Really, Carlos? Even if you're not religious, how can you not at least have heard to Paul the Apostle? That seems impossible! Surely some people you know are religious, right? Though he is from San Francisco. Maybe that's just how it is there.
  
  You quickly search Saul The Apostle on your phone and, yes, Mouse is right. "Nice job," you say, grinning, "I think that's it."
  *goto destination_choice
*else
  "I know," declares Mouse.
  
  Too fast! How does she do it?
  
  "I'm drawing a blank," says Carlos, shaking his head. "I think I might have watched too much TV to get this one. Can't think of any Sauls other than Goodman."
  
  You frown. "I thought it was Odenkirk?"
  
  "No, Odenkirk's the actor, the name of the character is Saul Goodman."
  
  You shrug. Yeah, that isn't it. Okay, so, what is?
  *goto destination_choice

*label destination_choice
*choice
  *disable_reuse *selectable_if (puzzle_share_immediately = false) #"Let's check out Monty Hall."
    *set target_hall "Monty Hall"
    *goto propose_wrong_hall
  *disable_reuse *selectable_if (puzzle_share_immediately = false) #"Let's check out Basket Hall."
    *set target_hall "Basket Hall"
    *goto propose_wrong_hall
  *disable_reuse *selectable_if (puzzle_share_immediately = false) #"Let's check out the Hall of the Americas."
    *set target_hall "Hall of the Americas"
    *goto propose_wrong_hall
  #"@{puzzle_share_immediately Okay, let's | Let's } check out the Hall of the Apostle."
    *return

*label propose_wrong_hall
*set times_proposed_wrong + 1
*if times_proposed_wrong = 1
  *goto propose_wrong_first_time
*elseif times_proposed_wrong = 2
  *goto propose_wrong_second_time
*elseif times_proposed_wrong = 3
  *goto propose_wrong_third_time
*else
  *bug "Somehow times_proposed_wrong was ${times_proposed_wrong}!"
  *goto destination_choice

*comment ########## Propose Wrong First Time ##########
*label propose_wrong_first_time
Carlos shrugs. "I'm stumped, so ${target_hall} is as good as any," he says.
*if puzzle_discuss_final_answer

  "It's Hall the Apostle," says Mouse, almost too quiet to hear.
  
  You look over at her and put on an encouraging smile. "Sorry, I didn't catch that. What did you get, Mouse?"
  
  "It's Hall the Apostle. Since Paul was also called Saul."
  
  You blink. "Really?" you say. "Was that what you were checking on your phone?"
  
  She nods.
  *choice
    #"That's better than what I had. Let's go to Hall the Apostle."
      *return
    #"Let's check out the ${target_hall} first, and we can come back to Paul the Apostle later. I mean, Hall the Apostle."
      Mouse nods in acceptance.
      *goto search_wrong_hall_first_time
*else
  Mouse shrugs, and offers no opinion.
  *goto search_wrong_hall_first_time

*label search_wrong_hall_first_time
The three of you go to ${target_hall},
*if (target_hall = "Monty Hall")
  which remains as beat up as ever.
  *goto search_wrong_hall_first_time_continue
*else
  which is every bit as beat up as your own dorm.
  
  You enter through the main doors, and it looks almost identical to your own, the only difference being that the colors are all more slightly red.
  *goto search_wrong_hall_first_time_continue
*label search_wrong_hall_first_time_continue
*set hours_remaining - 0.5

You trek up to room 310, but there aren't any paintings here. Instead, they've decorated the walls by hanging intricately patterned Tajik rugs. Wait, what? Holy shit! They look like Grandma's! They're beautiful! Who decides on the decorations? Is there somebody from Central Asia working here?

"Nah," says Carlos in disappointment, "this isn't it."

But look at the rugs, Carlos! No, he's not looking at the rugs. Mouse doesn't seem particularly interested, either. Well, you can't blame them. You'll have to come back up here later, but for now, time to go. "Okay," you say, "wasn't this one."
*goto destination_choice

*comment ########## Propose Wrong Second Time ##########
*label propose_wrong_second_time
*if puzzle_discuss_final_answer
  "You sure, Morgan?" asks Carlos. "I mean, you did say we'd check out Hall the Apostle next."
  
  You pause. Well, no, you said that you'd check it out later, but he has a point. You grin at Mouse. "All right," you say, "Hall the Apostle it is!"
  *return
*else
  "I'm throwing in the towel on this one," Carlos says. Mouse shakes her head. You're not sure if it's at Carlos or yourself.
  
  Either way, you did ask that they let you try by yourself, so ${target_hall} it is!
  *goto search_wrong_hall_second_time
*label search_wrong_hall_second_time
*set hours_remaining - 0.5

Why are all these buildings' exteriors so badly maintained, anyways? No use worry about it now, but at some point you've got to go ask somebody, maybe a teacher or something. Or are they professors now?

Look, that's not important. What's important is the fact that ${target_hall} also doesn't have any paintings on the third floor, instead sporting movie posters for Marvel movies. "Who picks the decor in these halls?" you ask, staring at a poster of The Batman.

"It's the RAs I think," says Carlos.

Well, that explains a lot. "Wrong twice," you say. "It's a coinflip now."
*goto destination_choice

*comment ########## Propose Wrong Third Time ##########
*label propose_wrong_third_time
"So," asks Carlos, "${target_hall}? Are you guessing, or do you have an actual idea?"

*fake_choice
  #"Carlos, come on. I never have an idea. I just do things, and most of the time it works."
    He laughs. "Well, at least you're honest."
    
    "One of my many virtues," you say. "I'm also super humble." You glance over at Mouse, and see her smiling slightly. "Anyways, let's get on with it."
  #"I do have an idea, but I thought I had an idea for the rest of them, too."
    He laughs. "Yeah, that's fair enough."
  #"I'm guessing."
    He laughs. "Well, at least you're honest."
*goto search_wrong_hall_third_time
*label search_wrong_hall_third_time
*set hours_remaining - 0.5

To your disappointment, ${target_hall} is not the correct hall. The RA has chosen strings of lights, and there's not a picture to be seen. That's three for three wrong.

You're beginning to regret your stance on hints, but there's nothing for it now! At this point, you can't help but be right!
*goto destination_choice

*comment ########################################
*comment # HALL THE APOSTLE                     #
*comment ########################################
*label sub_searching_hall_the_apostle
*page_break
The three of you tramp over to Hall the Apostle. It, like all the other dorms, is crumbling and overgrown, and like all the other dorms its floor plan is identical to Monty Hall's.


*finish