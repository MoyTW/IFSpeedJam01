*comment ########################################
*comment # WORK ON TARGET 3                     #
*comment ########################################
*label work_on_target_3
*gosub sub_discuss_building_3
*goto searching_hall_the_apostle

*comment ########################################
*comment # DISCUSSING THE BUILDING              #
*comment ########################################
*label sub_discuss_building_3
*temp target_hall ""
*temp times_proposed_wrong 0

You read the clue out loud. "Which dorm has the strongest association with Saul?"

"Saul?" asks Carlo. "Like, Bob Odenkirk?"

"Sorry, who?"

"The actor who pays Saul Goodman in Breaking Bad and Better Call Saul." Carlo grins. "Good shows, by the way. You ever seen them?"

*choice
  #Gremlin: "Isn't Breaking Bad really old? Do you only watch old shows and movies, Carlo, or have you watched anything made in the last ten years?"
    "Ah," says Carlo, "actually, Breaking Bad ended in 2013. You should always double-check your barbs before you throw them."
    
    "Really? You memorize the year dates for the old shows you watch?"
    
    "You can't hold my amazing memory against me. And you keep calling it old. Morgan, 8 years isn't that old. My grandfather is still going at strong 97. It only seems old because you're so young."
    
    You can see Mouse struggling to suppress a grin. "Gah," you say, "they're right, Boomer really is a state of mind."
    
    Mouse breaks out in a fit of laughter as Carlo places a hand on his heart. "I am wise beyond my years," he intones.
    
    "Okay but seriously," you say, "I don't think it's the guy from Breaking Bad."
    
    "Yeah," he says, "you're probably right."
    
    After her giggles subside,
    *goto mouse_unfolds_her_map
  #Considerate: "I haven't. I don't watch many of the older shows, but I hear that they're regarded highly."
    "They're very good," he says. Then he pauses. "That's probably not what they're going for in this scavenger hunt."
    
    You shrug. "Either way's possible, but I've never seen it. Does it seem like it'd be relevant to any of the dorms?"
    
    "No," he says, "not at all."
    
    *goto mouse_unfolds_her_map
  #Nonplussed: "No. Still, I don't think they would make a pop-culture reference to a show that's out of our age range."
    "You're probably right." He unfolds his map and scans the dorm names. "You're definitely right," he says.
    
    *goto mouse_unfolds_her_map
*label mouse_unfolds_her_map
Mouse unfolds her map, nods, and starts typing into her phone. Well, she's clearly got ideas. On the other hand, Carlo looks pretty stumped. You stare at your own map. Monty Hall, Basket Hall, Hall of the Americas, Hall the Apostle…who named these dorms? These can't be real names. Well, Monty Hall and Basket Hall could be, but the others?

*if puzzle_share_immediately
  "Hall the Apostle," declares Mouse.
  
  "Wait," you say, "are you talking about Paul the Apostle?"
  
  "He was also called Saul," she clarifies. "I looked it up."
  
  You look over at Carlo. He shrugs. "I don't even know who Paul the Apostle is. My parents weren't religious."
  
  Really, Carlo? Even if you're not religious, how can you not at least have heard to Paul the Apostle? That seems impossible! Surely some people you know are religious, right? Though he is from San Francisco. Maybe that's just how it is there.
  
  You quickly search Saul The Apostle on your phone and, yes, Mouse is right. "Nice job," you say, grinning, "I think that's it."
  *goto destination_choice
*else
  "I know," declares Mouse.
  
  Too fast! How does she do it?
  
  "I'm drawing a blank," says Carlo, shaking his head. "I think I might have watched too much TV to get this one. Can't think of any Sauls other than Goodman."
  
  You frown. "I thought it was Odenkirk?"
  
  "No, Odenkirk's the actor, the name of the character is Saul Goodman."
  
  You shrug. Yeah, that isn't it. Okay, so, what is?
  *goto destination_choice

*label destination_choice
*image campusmap.png center campus map
*choice
  *disable_reuse *selectable_if (puzzle_share_immediately = false) #"Let's check out Monty Hall."
    *set target_hall "Monty Hall"
    *goto propose_wrong_hall
  *disable_reuse *selectable_if (puzzle_share_immediately = false) #"Let's check out Basket Hall."
    *set target_hall "Basket Hall"
    *goto propose_wrong_hall
  *disable_reuse *selectable_if (puzzle_share_immediately = false) #"Let's check out the Hall of the Americas."
    *set target_hall "Hall of the Americas"
    *goto propose_wrong_hall
  #"@{puzzle_share_immediately Okay, let's | Let's } check out the Hall of the Apostle."
    *return

*label propose_wrong_hall
*set times_proposed_wrong + 1
*if times_proposed_wrong = 1
  *goto propose_wrong_first_time
*elseif times_proposed_wrong = 2
  *goto propose_wrong_second_time
*elseif times_proposed_wrong = 3
  *goto propose_wrong_third_time
*else
  *bug "Somehow times_proposed_wrong was ${times_proposed_wrong}!"
  *goto destination_choice

*comment ########## Propose Wrong First Time ##########
*label propose_wrong_first_time
Carlo shrugs. "I'm stumped, so ${target_hall} is as good as any," he says.
*if puzzle_discuss_final_answer

  "It's Hall the Apostle," says Mouse, almost too quiet to hear.
  
  You look over at her and put on an encouraging smile. "Sorry, I didn't catch that. What did you get, Mouse?"
  
  "It's Hall the Apostle. Since Paul was also called Saul."
  
  You blink. "Really?" you say. "Was that what you were checking on your phone?"
  
  She nods.
  *choice
    #"That's better than what I had. Let's go to Hall the Apostle."
      *return
    #"Let's check out the ${target_hall} first, and we can come back to Paul the Apostle later. I mean, Hall the Apostle."
      Mouse nods in acceptance.
      *goto search_wrong_hall_first_time
*else
  Mouse shrugs, and offers no opinion.
  *goto search_wrong_hall_first_time

*label search_wrong_hall_first_time
*gosub_scene ch4_hunt func_advance_half_hour
The three of you go to ${target_hall},
*if (target_hall = "Monty Hall")
  which remains as beat up as ever.
  *goto search_wrong_hall_first_time_continue
*else
  which is every bit as beat up as your own dorm.
  
  You enter through the main doors, and it looks almost identical to your own, the only difference being that the colors are all more slightly red.
  *goto search_wrong_hall_first_time_continue

*label search_wrong_hall_first_time_continue

You trek up to room 310, but there aren't any paintings here. Instead, they've decorated the walls by hanging intricately patterned Tajik rugs. Wait, what? Holy shit! They look like Grandma's! They're beautiful! Who decides on the decorations? Is there somebody from Central Asia working here?

"Nah," says Carlo in disappointment, "this isn't it."

But look at the rugs, Carlo! No, he's not looking at the rugs. Mouse doesn't seem particularly interested, either. Well, you can't blame them. You'll have to come back up here later, but for now, time to go. "Okay," you say, "wasn't this one."

*gosub_scene ch4_hunt func_print_time_readout
*goto destination_choice

*comment ########## Propose Wrong Second Time ##########
*label propose_wrong_second_time
*if puzzle_discuss_final_answer
  "You sure, Morgan?" asks Carlo. "I mean, you did say we'd check out Hall the Apostle next."
  
  You pause. Well, no, you said that you'd check it out later, but he has a point. You grin at Mouse. "All right," you say, "Hall the Apostle it is!"
  *return
*else
  "I'm throwing in the towel on this one," Carlo says. Mouse shakes her head. You're not sure if it's at Carlo or yourself.
  
  Either way, you did ask that they let you try by yourself, so ${target_hall} it is!
  *goto search_wrong_hall_second_time

*label search_wrong_hall_second_time
*gosub_scene ch4_hunt func_advance_half_hour

Why are all these buildings' exteriors so badly maintained, anyways? No use worry about it now, but at some point you've got to go ask somebody, maybe a teacher or something. Or are they professors now?

Look, that's not important. What's important is the fact that ${target_hall} also doesn't have any paintings on the third floor, instead sporting movie posters for Marvel movies. "Who picks the decor in these halls?" you ask, staring at a poster of The Batman.

"It's the RAs I think," says Carlo.

Well, that explains a lot. "Wrong twice," you say. "It's a coinflip now."

*gosub_scene ch4_hunt func_print_time_readout
*goto destination_choice

*comment ########## Propose Wrong Third Time ##########
*label propose_wrong_third_time
"So," asks Carlo, "${target_hall}? Are you guessing, or do you have an actual idea?"

*fake_choice
  #"Carlo, come on. I never have an idea. I just do things, and most of the time it works."
    He laughs. "Well, at least you're honest."
    
    "One of my many virtues," you say. "I'm also super humble." You glance over at Mouse, and see her smiling slightly. "Anyways, let's get on with it."
  #"I do have an idea, but I thought I had an idea for the rest of them, too."
    He laughs. "Yeah, that's fair enough."
  #"I'm guessing."
    He laughs. "Well, at least you're honest."
*goto search_wrong_hall_third_time

*label search_wrong_hall_third_time
*gosub_scene ch4_hunt func_advance_half_hour

To your disappointment, ${target_hall} is not the correct hall. The RA has chosen strings of lights, and there's not a picture to be seen. That's three for three wrong.

You're beginning to regret your stance on hints, but there's nothing for it now! At this point, you can't help but be right!

*gosub_scene ch4_hunt func_print_time_readout
*goto destination_choice

*comment ########################################
*comment # HALL THE APOSTLE                     #
*comment ########################################
*label searching_hall_the_apostle
*temp proposed_painting ""
*page_break
The three of you tramp over to Hall the Apostle. It, like all the other dorms, is crumbling and overgrown, and like all the other dorms its floor plan is identical to Monty Hall's. You take the elevator up to the third floor and walk over to room 310. Sure enough, the hallway is decorated in various paintings.

"Right," you say, "the clue says it's behind 'the painting most related to Saul's dramatic escape.'" You glance down the hallway. There are five paintings, or rather, prints of paintings, all on the wall opposite the windows. You take a slow walk down the corridor.

The first one is some sort of Native American pyramid with stepped walls, all overgrown with vines, surrounded by a verdant jungle. Honestly, it's kind of like how the dorms look from the outside.

The second is a vaguely Renaissance-ish painting of an idealized farmhand or maid or something similar, carrying a gigantic wicker basket of laundry. She's not wearing any shoes, she's incredibly clean, and in general it all just looks like it was painted during that phase when all those rich Europeans were going gaga over rural life. Come on, old rich painting-commissioning Europeans. That's now how somebody who actually works would look. At least give the poor girl some shoes!

Anyways, the third one is of a modern city! Actually, most of it is of a very long red suspension bridge, but the city in the background has some skyscrapers in it. The day is clear, and the bridge is full of very detailed cars, but it's pretty unremarkable otherwise. For what it's worth, the water's very pretty.

The fourth is a very dignified man in a study in late 1800s formalwear, bent over a fancy desk. On it is a sheet of paper, and in his hand is a quill. He has a top hat next to him, but he's not Abraham Lincoln. Not to say that only Abraham Lincoln wore a top hat, of course. If this is supposed to be a famous person, you have no idea who it is.

The last is a old-timey paddle ship on a river. What are they called? Steam boats? There are well-dressed ladies and gentlemen on the deck, and the riversides are awash in green.

"Well," says Carlo, looking over the hangings, "none of these seem particularly dramatic or escape-y to me."

Mouse puts down her phone. Has she been searching something again? "I know," she says.

Well, at this point, it'd be more shocking if Mouse didn't figure it out.
*if puzzle_share_immediately
  *goto immediate_discussion
*else
  *goto delayed_or_no_discussion

*comment ########## Immediate Discussion ##########
*label immediate_discussion
You look over to Mouse and raise an eyebrow.
  
"It's the laundry basket."

Laundry basket? Paul, laundry basket, Paul, escape, Paul…come on, did all that Bible Study just fall right out of your head, Morgan? What dramatic escapes did Paul have? Nope. Drawing a blank. Apparently it did all fall out of your head. Too bad the hint wasn't about the Song of Solomon, you'd have gotten that one.

Think back, Morgan! Remember those days of Bible Study, where, well, it was nice, you got to see friends, but the teaching methods were kind of dull. Something more engaging would probably have been better. The content was interesting, but the presentation…not.
*choice
  #"All right, let's put it behind the laundry basket then."
    *goto put_sticker_behind_farmhand
  #"All right, I give. How so?"
    She consults her phone. "He escaped from Damascus by being lowered in a basket."

    Shit, yes, of course, how did you forget that, other then the fact that it's hardly ever come up and you can barely remember it in the first place? Were you just meant to Google everything for this one? Well it worked for Mouse.

    *goto put_sticker_behind_farmhand
  #"I think it's something else."
    *goto i_think_its_something_else

*label i_think_its_something_else
Mouse looks at you with curious eyes. "Which one?"
*choice
  #"The first one, the overgrown pyramid."
    *set proposed_painting "the overgrown pyramid"
    *goto mouse_what_does_okay_mean
  #"The third one, the red bridge to the city."
    *set proposed_painting "the Golden Gate bridge"
    *gosub the_golden_gate_bridge_snippet
    
    "Well, fair enough," he says, turning to Mouse. "You think that's the one?"

    *goto mouse_what_does_okay_mean
  #"The fourth one, the man writing a letter."
    *set proposed_painting "the man writing a letter"
    *goto mouse_what_does_okay_mean
  #"The fifth one, the paddle ship on a river."
    *set proposed_painting "the paddle ship on a river"
    *goto mouse_what_does_okay_mean

*label mouse_what_does_okay_mean
Mouse looks at the painting of ${proposed_painting}, then back at you, then back at the farmhand. "Okay," she says.

Mouse, does that mean okay, I see it, or does that mean, okay, I disagree, but I'm too shy and conflict averse to actually contradict you? Speak up, Mouse! Explain your intentionally ambiguous statements! 

It's definitely the second one, though, and as much as you'd like her to speak up hounding her would only hurt. You put on a bright smile. "Okay," you say.
*choice
  #Put the sticker behind the farmhand painting.
    *goto put_sticker_behind_farmhand
  #Put the sticker behind the painting of ${proposed_painting}.
    *goto put_sticker_behind_proposed_painting

*comment ########## Delayed Or No Discussion ##########
*label delayed_or_no_discussion
*choice
  #"I think it's the first one, the overgrown pyramid."
    *set proposed_painting "the overgrown pyramid"
    *goto finally_discuss_or_put_if_no_discussion
  #"I think it's the second one, the girl with a laundry basket."
    Mouse nods. "I think so too," she says.

    "Really?" asks Carlo. "If you both agree, it's definitely right. Let's get this one finished then."

    *goto put_sticker_behind_farmhand
  #"I think it's the third one, the red bridge into a city."
    *set proposed_painting "the Golden Gate Bridge"
    *gosub the_golden_gate_bridge_snippet
    "All that aside," you say, "I think it's that one."

    *goto put_sticker_behind_proposed_painting
  #"I think it's the fourth one, the man writing a letter."
    *set proposed_painting "the man writing a letter"
    *goto finally_discuss_or_put_if_no_discussion
  #"I think it's the fifth one, the paddle ship on a river."
    *set proposed_painting "the paddle ship on a river"
    *goto finally_discuss_or_put_if_no_discussion

*label finally_discuss_or_put_if_no_discussion
*if puzzle_discuss_final_answer
  Mouse shakes her head, then points to the painting of the girl with the wicker basket. "That one," she says.

  You blink. "Can you explain to us why?" you ask her.

  She consults her phone. "Paul escaped from Damascus by being lowered in a basket."

  Well, apparently all those Bible Study sessions all just fell right out of your head. You do a quick search on your phone for "paul damascus basket" and yep, there are a million and one pictures of Paul being lowered over all wall in a basket.

  *choice
    #Put the sticker behind the painting of the girl with the wicker basket.
      *goto put_sticker_behind_farmhand
    #Put the sticker behind the painting of ${proposed_painting}
      *goto put_sticker_behind_proposed_painting
*else
  Carlo shrugs. "Good enough as any, he says." Mouse shrugs too, saying nothing.

  "All right," you say. "The painting of ${proposed_painting} it is, but what did you get, Mouse?"
  
  "The farmhand," she says, "since Paul escaped Damascus in a basket."

  Well, apparently all those Bible Study sessions all just fell right out of your head. "Good catch," you say.

  Carlo laughs. "Hey, you can't change your answer now, Morgan."

  "Don't worry, I'm not going to!" You grin.
  *goto put_sticker_behind_proposed_painting

*comment ########## Shared Labels And Endings ##########

*label the_golden_gate_bridge_snippet
"Wait," says Carlo. "Do you mean the painting of the Golden Gate Bridge?"
    
"If that's what it is," you reply, "yes."

"Huh. I thought the Golden Gate Bridge was nationally famous."

You shrugs. What can you say to that? Sorry, Carlo, I'm just not a bridge type of girl? Sorry, Carlo, your local landmarks aren't nationally famous? Sorry, Carlo, I haven't traveled much? You settle for, "Sorry, I wasn't familiar enough to recognize it, but I have heard of it." Which is true!
*return

*label put_sticker_behind_farmhand
*set sticker_used_3 true
*set sticker_placed_correctly_3 true
"Carlo," you say, gesturing to the painting of the girl with the laundry basket, "can you hold that?"

"Sure thing," he says, holding it up. You peel off a sticker and smooth it onto the wall behind the painting.
*gosub_scene ch4_hunt func_advance_hour
*goto_scene ch4_hunt correctly_placed_sticker

*label put_sticker_behind_proposed_painting
*set sticker_used_3 true
*set sticker_placed_correctly_3 false
"Carlo," you say, gesturing to the painting of ${proposed_painting}, "can you hold that?"

"Sure thing," he says, taking it off the wall. You peel off a sticker and smooth it onto the wall behind the painting, and Carlo replaces it, straightening it with care. "Okay, let's get out of here," you say.
*gosub_scene ch4_hunt func_advance_hour
*goto_scene ch4_hunt work_on_a_clue