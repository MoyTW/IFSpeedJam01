*set inventory_has_backpack true
*set inventory_has_book true
*set inventory_has_paperwork false
*set inventory_has_scavenger_hunt_sheet true
*set inventory_has_scavenger_hunt_stickers true
*set inventory_has_campus_map true
*set inventory_has_student_id true

"So," you say, in between bites of your breakfast omelette, "it's
*if (start_time = 1000)
  *set current_time start_time
  9:47 right now,
  *goto discuss_time_continue
*else
  *set current_time start_time
  10:47 right now,
  *goto discuss_time_continue
*label discuss_time_continue
and it finishes at 8:00 this evening, but I'm not super set on winning it or anything." You raise an eyebrow at Carlo, who nods, and then look at Mouse, who also seems to assent. "So, how about we start at the top of the hour, eat lunch at around 12:30, and then come back for dinner at 6:30? That sound good?"

"No problems here," says Carlo. He spoons some of his granola cereal stuff into his mouth.

"Mouse?" you ask. She nods.

"All right then," you say, putting the hint sheet down on the table. "So, it looks like you first go to a building, then you find something inside of the building, and then you put a sticker where it says."

Carlo eyes the sheet. "Well," he says, "these don't seem too bad. How invested are you in solving these on your own, Morgan, or do you mind us just blurting out whatever comes into our heads?"

*gosub select_difficulty_snippet

"That's fine by me," says Carlo. You check with Mouse, and she nods.

"All right," you say, polishing off your plate, "as soon as you guys are ready."
*goto work_on_a_clue

*comment ########################################
*comment # WORK ON A CLUE                       #
*comment ########################################
*label work_on_a_clue
*temp num_stickers_used 0
*gosub func_set_num_stickers_used
*if (num_stickers_used = 5)
  *goto all_stickers_used
*elseif ((lunch_eaten = false) and (current_time >= 1230))
  *goto lunch
*elseif current_time >= 1800
  *goto out_of_time
*else
  *goto select_next_target

*comment ########## Out of Time ##########
*label out_of_time
*page_break
Carlo checks his watch. "Hey Morgan," he says, "what would you say to dinner?"

"What time is it?" you ask.

"It's
*gosub func_print_time false
right now."

*if current_time < 1830
  "Didn't we say half past six?"
  
  "Yeah," he says, "but we've been taking an hour per clue, so I figure we may as well call it now."
  
  You shrug. "Sure," you say, "why not?
  *goto agree_get_dinner
*else
  "All right," you say, "let's go get dinner.
  *goto agree_get_dinner
*label agree_get_dinner
Can we stop at the dorms first, though? I want to change."

"Sure," he says. Mouse nods. The three of you go back to the dorm, and then Mouse vanishes into her room. You go back to yours and change, and the three of you go to dinner.
*page_break
*goto ending_b_blame_alex

*comment ########## Lunch ##########
*label lunch
*set lunch_eaten true
*page_break
*if current_time = 1230
  *gosub func_advance_hour
  You check your phone. Oh, it's 12:30. "All right," you say, "you guys want to get lunch?"
  
  Carlo grins. "Sounds great," he says. You look over at Mouse, who nods her assent.
  
  "All right," you say, "lunch it is!"
  
  The three of you go to the student center, which is swarming with students for the lunch rush. It's hard to find a free table, so you have to settle for one end of one of the longer ones. You have a pretty leisurely lunch, chatting about your favorite books. Carlo doesn't seem to read a ton, and what he does read tends towards fantasy. Mouse only talks a little. You end up mostly sharing tidbits about the origins of Wicca.
  *goto work_on_a_clue
*elseif current_time >= 1400
  *gosub func_advance_half_hour
  "Hey," says Carlo, "did we miss lunch?"
  
  You check your phone. It's 
  *gosub func_print_time false
  now. "Wow, that took longer than I thought it would," you say. "I think they stop serving at two, so we've missed lunch, yeah. Sorry."
  
  "No worries," he says, "we can go grab something at the student store, I think they have premade sandwiches and stuff."
  
  "Sorry," you repeat, "I didn't mean to take that long."
  
  The three of you go on over to the student store and grab various sandwiches and snacks, and have an impromptu picnic on the green between the dorms before returning to the hunt.
  *goto work_on_a_clue
*else
  *gosub func_advance_hour
  You check your phone. Oh, you've blown right past 12:30. "All right," you say, "you guys want to get lunch?"
  
  Carlo grins. "Sounds great," he says. You look over at Mouse, who nods her assent.
  
  "All right," you say, "lunch it is!"
  
  The three of you go to the student center, where the first wave of students is beginning to subside. You claim a table and have a pretty leisurely lunch, chatting about your favorite books. Carlo doesn't seem to read a ton, and what he does read tends towards fantasy. Mouse only talks a little. You end up mostly sharing tidbits about the origins of Wicca.
  *goto work_on_a_clue

*comment ########## Select Next Target ##########
*label select_next_target
*page_break
*gosub func_print_time_readout

[b][i][
*if ((puzzle_share_immediately = true) and (puzzle_discuss_final_answer = true))
  You've asked that Mouse and Carlo tell you the solutions to each hint.
  *goto difficulty_desc_continue
*elseif (puzzle_discuss_final_answer = true)
  You've asked that Mouse and Carlo let you guess, but inform you if you guessed wrong.
  *goto difficulty_desc_continue
*else
  You've asked that Mouse and Carlo not help you.
  *goto difficulty_desc_continue
*label difficulty_desc_continue
][/b][/i]

*gosub_scene clue_sheet clues_only
*choice
  *selectable_if (sticker_used_1 = false) #Work on the first target.
    *goto_scene target_1 work_on_target_1
  *selectable_if (sticker_used_2 = false) #Work on the second target.
    *goto_scene target_2 work_on_target_2
  *selectable_if (sticker_used_3 = false) #Work on the third target.
    *goto_scene target_3 work_on_target_3
  *selectable_if (sticker_used_4 = false) #Work on the fourth target.
    *goto_scene target_4 work_on_target_4
  *selectable_if (sticker_used_5 = false) #Work on the fifth target.
    *goto_scene target_5 work_on_target_5
  #"Actually, I want to change how we're handling the discussions."
    "Sure," says Carlo. "What did you have in mind?"
    *gosub select_difficulty_snippet
    *goto work_on_a_clue

*label select_difficulty_snippet
*fake_choice
  #"I want to try and solve them all on my own. Don't tell me anything until I've placed the sticker, even if you think I'm making a mistake." [i][ If you choose this, Mouse and Carlo will not help you. ][/i]
    *set puzzle_share_immediately false
    *set puzzle_discuss_final_answer false
  #"I want to give them a try, so don't spoil me if you think you know, but we should all discuss before I actually place a sticker." [i][ If you choose this, Mouse and Carlo will inform you if you guessed wrong. ][/i]
    *set puzzle_share_immediately false
    *set puzzle_discuss_final_answer true
  #"If you have an idea, say it right then and there. I'm not particularly bothered about puzzle spoilers." [i][ If you choose this, Mouse and Carlo will tell you the solutions to each hint. ][/i]
    *set puzzle_share_immediately true
    *set puzzle_discuss_final_answer true
*return

*comment ########## All Stickers Used ##########

*label all_stickers_used
"That was number five," says Carlo.

"Oh," you say, "right." You check the time. "Well," you say, "you want to head back to the dorms? I want to get changed before dinner, if we have the time. We can get food a little later and still make the raffle at 8:30."

"Sure," he says. Mouse nods. The three of you go back to the dorm, and then Mouse vanishes into her room. You go back to yours and change, and the three of you go to dinner.

*page_break
*goto ending_b_blame_alex

*comment ########################################
*comment # CORRECTLY PLACED STICKER             #
*comment ########################################
*label correctly_placed_sticker
*temp num_stickers_placed_correctly 0
*if sticker_placed_correctly_1
  *set num_stickers_placed_correctly + 1
*if sticker_placed_correctly_2
  *set num_stickers_placed_correctly + 1
*if sticker_placed_correctly_3
  *set num_stickers_placed_correctly + 1
*if sticker_placed_correctly_4
  *set num_stickers_placed_correctly + 1
*if sticker_placed_correctly_5
  *set num_stickers_placed_correctly + 1

*if num_stickers_placed_correctly = 1
  "Now that that's sorted," says Carlo, "which one are we doing next, ${player_first_name}?"
  
  "Sorry," you say, "what did you say?"
  
  "I said, which one are we doing next?"
  
  "No, no," you say, "what did you say after that?"
  
  "Uh." Carlo blinks. "Morgan."
  
  You stare at him. "Yes?"
  
  "I said Morgan. After I asked you which one we're doing next. Sorry, uh, is this a bit? Like, who's on first?"
  
  "It's not," you say, "I'm just hearing things." Who's on what? You shake your head. Probably an old movie or something. Asking about that would just make it seem even more like a bit. "All right, as for the next one, give me a second to pick."
  *goto work_on_a_clue
*elseif num_stickers_placed_correctly = 2
  All right, now that that's sorted, you - whoa, you're dizzy, when did that happen? You try to reach out to the wall to steady yourself, but your arm isn't in the right place, and you sort of collapse into the wall, which is good, because if you'd missed completely then you'd be on the floor. You lean on the wall for a few seconds, and reality reasserts itself.
  
  "Morgan," asks Mouse, "are you okay?"
  
  You push yourself off the wall. That was weird. You shift your head around a bit, and everything feels normal. "Yeah," you say. "I'm fine." Well, fine now, at least. You force a smile. "Must not have gotten enough sleep. All right, back to the hunt."
  *goto work_on_a_clue
*elseif num_stickers_placed_correctly = 3
  
  You're sweating. How did you not nice that you were sweating? And you're breathing hard, too, and you can feel your heart thumping, and now your eyes sting. When did this happen? It's not super uncomfortable, you've definitely had worse after working out, but the worst you've done today is some leisurely walking.
  
  You reach up to brush the sweat off your forehead, but it's dry, and you suddenly feel a sense of release. Is this some sort of panic attack or something? Did you fall sick and not notice it? Neither Carlo nor Mouse seem to have noticed it.
  
  You're…okay, right? Yes. You're fine. "All right," you say, your voice coming out surprisingly chipper, "that one's down, let's move on."
  *goto work_on_a_clue
*elseif num_stickers_placed_correctly = 4
  *goto correct_sticker_number_4
*elseif num_stickers_placed_correctly = 5
  *goto_scene ch5_madness the_spell_is_complete
*else
  *bug When going to correctly_placed_sticker, number of placed stickers was ${num_stickers_placed_correct}, but it should have been between 1-5 (inclusive).

*comment ########## Correct Sticker Number 4 ##########
*label correct_sticker_number_4
Your right eye itches. You blink. The itch goes away, but now there's some sort of pressure. It's very uncomfortable, actually, kind of like when you don't get enough sleep and you're trying to wake up and your eye refuses to open, except now it's growing, and it feels like your eye's being squeezed, and now the back of your head feels like it's being squeezed, and it keeps squeezing, and your eye is compacting, you can feel your skull creaking, and you drop down to the floor, and you're screaming, and
  
it's gone

and now there's a tingling in your spine, and oh no, it's spreading, and it reaches down through your arms and legs, and the tingling starts to intensify and your nerves are burning and screaming and you're screaming too

and then you slump over, the world turns turns sideways

and you're back, standing where you were. Carlo is waving his hand in front of your face. "Hey, are you okay?" he asks.

You're fine. You're fine. You can breathe. It's fine. Somehow you're standing up. You calm your ragged breaths. You're fine, your body's fine, whatever that was has passed and you're fine now.

*label after_body_horror
*choice
  *hide_reuse #"What just happened?"
    Carlo frowns. "Well, you got real quiet, and didn't respond when I asked where you were thinking of next." He thinks on his next words carefully. "It looked kind of like you were having a nightmare, but standing up, and with your eyes open. Are you sure you're okay?"
    *goto after_body_horror
  #"I'm fine. Sorry, I just got distracted."
    *gosub mouse_asks_if_youre_sure_snippet
    *choice
      #"I'm sure. Thanks for asking, Mouse. I appreciate it."
        Mouse nods, and Carlo raises his eyebrow. "Well," he says, "if you're sure. You don't want to go sit down or anything?"

        "No," you say, "let's just go get this last target."
        *goto work_on_a_clue
      #"Mostly. Thanks for asking, Mouse. I think I'll sit down for a bit, then we can finish up this hunt."
        *gosub sit_on_bench_then_leave_snippet
        *goto work_on_a_clue
      #"No. Thanks for asking, Mouse. Do they have a nurse's office here? It might be a good idea to pay it a visit."
        *goto do_they_have_a_nurses_office
  #"Sorry, I think I'm kind of having a panic attack.  I'm going to go find somewhere to sit down for a minute."
    *gosub sit_on_bench_then_leave_snippet

    *gosub mouse_asks_if_youre_sure_snippet
    *choice
      #"I'm sure. Thanks for asking, Mouse. I appreciate it."
        She nods. "Okay," she says.
        
        "Let's get moving," says Carlo. "We're almost done, one more to go."
        *goto work_on_a_clue
      #"No, actually, I'm not sure. Do they have a nurse's office here on campus?"
        *goto do_they_have_a_nurses_office
  #"No. I'm not. I think I'm having some sort of panic attack. Do they have a nurse's office here?"
    *goto do_they_have_a_nurses_office

*label mouse_asks_if_youre_sure_snippet
Mouse walks up to you. She doesn't quite meet your eyes, but you can tell she's making an effort. "Morgan," she says, "are you sure?"

This is touching, Mouse, I'm not even joking. I appreciate that you're trying so hard, but I'm not about to die or anything. This kind of stuff has never happened before, but it wasn't that bad, right? I mean, I didn't fall over or anything. It's just a little nerves, right?

Right?
*return

*label sit_on_bench_then_leave_snippet
You go outside and find a bench near the door. It's clean at least, though it looks ancient and worn. You sit for a bit.

"Want a drink?" asks Carlo, pulling some sort of drink pouch out of his trenchcoat.

"Sure," you say, taking it and peeling off the straw. "How do you use this?" you ask.

"Punch the straw through that little circle on top."

"Thanks," you say. You push in the straw and slurp up the drink. It's sweet and tangy. Neither Mouse nor Carlo speaks as you sip at the drink, but you can feel the concern in their eyes. When you've finished, you look around, then spot a trash can. You ball up the pouch and toss it in. "All right," you say, standing up. "Let's get going."
*return

*label do_they_have_a_nurses_office
"Hold on," says Carlo, pulling out his welcome packet, "I'm sure there's something in here about that." He flips through the packet, scanning the pages.
      
Mouse looks up from her phone and speaks. "It's in the Admin Building."

You check your breathing. You're fine, now. It's fine, right? It's just, it's not bad. Some anxiety is to be expected, right, it's your first day at college, that's a big step for everyone, right? It's not bad enough to go to the medical office. Right? There's nothing wrong with you, you don't need to go see the doctor, you didn't even fall down or anything.
*choice
  #"Sorry for making you guys worry, but I feel fine now. I appreciate the concern, but I think I can go check it out later, on my own, so let's keep going for now. All right?"
    Carlo and Mouse look at each other. Guys, you don't need to worry so much. I'm fine. I'm serious. "Well," says Carlo, "if you say so. Where to next?"
    *goto work_on_a_clue
  #"Sorry for bailing, guys, but I think I'm going to go to the nurse's office to talk to somebody. These panic attack things are new for me, I've never had anything like them before."
    "What," says Carlo, "you were worried about bailing?"
    
    "Yes, of course. Among other things."
    
    He chuckles. "Uh, I think your priorities are out of order, Morgan. You should definitely go to the nurse's office." You look over to Mouse, and she nods emphatically. "Look," he says, "would you be happy if somebody you just met put off going to see somebody about their panic attacks just to be polite to you?"
    
    You sigh. "Yeah," you say, "you're right."
    *goto ending_a_a_passing_madness

*comment ########################################
*comment # ENDING SEQUENCES             #
*comment ########################################

*label ending_a_a_passing_madness
[b]Ending A: A Passing Madness[/b]

You drop the scavenger hunt to go to the nurse's office, a small complex in the back of the Admin Building. You describe your issues to the receptionist then sit down in the empty lobby to wait, telling Carlo and Mouse that they don't need to wait. Carlo goes off to do dinner, but Mouse decides to stay. You chat a little in the lobby, and it seems she's much more forthcoming when it's a one-on-one conversation. She even talks at a volume close to normal.

Soon after you're seen by Joshua Graham, a nurse practitioner, who asks you a bunch of questions, has you move around a bit, and then tells you that you should rest and come back if you have any other issues. You're not sure how well you can measure somebody's health just through a quick survey, but it's reassuring to hear that there's nothing obviously wrong with you.

It takes only about forty minutes total, short enough that you and Mouse have no issue getting dinner before it closes.

You hang out for a couple more hours before returning to your room. These last couple of days have been pretty eventful, but they could have gone worse. You fall asleep with little trouble.
*page_break
*goto bad_end_next_four_years

*label ending_b_blame_alex
[b]Ending B: Blame Alex[/b]

"It's a reference to Glengarry Glen Ross," says Carlo, pointing to the rules sheet. "The prize thing, I mean." He looks over at Mouse. "If you search Cadillac, steak knives, you're fired, you'll get the quote."

"All right," you say, "but what does it have to do with sales?"

"Well, the movie's about salespeople. The plot is that -"

"Hold on," you say, "My brother just texted me." You check the text. [i]Alex: Hey Morgan, did you place all the stickers?[/i] "He's asking about the stickers."

"Oh?" asks Carlo. "Does he know the answers?"

"Dunno, let me answer." [i]Morgan: Yeah, why?[/i]

[i]Alex: Okay let me be honest Morgan the scavenger hunt was a lie. Well, specifically, me and my friends made it up, give you something to do. There's no prizes. You've just been stickering random things.[/i]

You sigh. "Hey," you say, "guess what?" Mouse looks up from her pasta.

Carlo frowns. "He found one of your stickers in the wrong place, and decided to text you about it?"

"No. He's saying that there's no raffle, and that the whole thing was just something he and his friends made up and gave to me so I'd have something to do."

"Okay I have to say, I didn't expect that." Carlo laughs. "Damm, Morgan! You have to admit, that's pretty funny!"

You shake your head. "I'm not going to argue that it's not." You sigh. "You know, it was fun. So I'll let it pass, but I'm gonna make him clean those stickers up."

"Only fair," says Carlo, grinning. "So, what, there's no raffle?"

"I guess not," you say. "Anyways, what were you saying?"

"So, the plot of the movie is that…"
*page_break
*goto bad_end_next_four_years

*label bad_end_next_four_years
The next four years were very strange.

You graduated on time. You made friends, went to parties, studied reasonably hard, and at the end, you walked out and took your diploma with all the rest of your classmates. But the whole time, it felt slightly unreal.

The campus, for example. Why had it seemed so run down on the outside? Hallucinations, or something similar. What you saw was not what anybody else saw. Where you saw weeds and wild shrubs, others saw well-maintained gardens. Where you saw crumbling brickwork, they saw beautiful facades. It only happened outdoors, and only around the campus, a strange kind of madness.

The weather, too, was often different. Sunny days were grim, and dark ones cheerful. You dressed against the weather often enough to get a minor reputation for it. The rain was the strangest thing. It'd rain with no clouds in the sky. You took a picture, once, and the picture showed stormy clouds even while you felt the warmth of the sun on your cheeks. It was novel, if nothing else.

You seem to have succeeded in hiding it from almost everybody you met, covering your hallucinations in excuses of eccentricity and quirkiness. Carlo may have had his suspicions, but you only ever confided in Mouse. Without her, you think you might have gone mad. You're still not sure you weren't.

You count yourself lucky, though. It only happened on the campus. When you went home, everything was as it should be. When you drove into town, or visited Carlo in San Francisco, or stayed with Mouse over the summer, everything was normal. And so, you graduated, and left college behind, and with it your strange but harmless madness.

Sometimes, you wonder. What would it look like if you went back? But it's far off, and there's nothing more there for you, so you never wonder long.
*finish

*comment ########################################
*comment # UTILITY FUNCTIONS             #
*comment ########################################
*label func_advance_half_hour
*if (current_time modulo 100) = 0
  *set current_time + 30
  *return
*else
  *set current_time + 70
  *return

*label func_advance_hour
*set current_time + 100
*return

*comment Really? We can't assign return values? ChoiceScript why u do dis.
*label func_set_num_stickers_used
*set num_stickers_used 0
*if sticker_used_1
  *set num_stickers_used + 1
*if sticker_used_2
  *set num_stickers_used + 1
*if sticker_used_3
  *set num_stickers_used + 1
*if sticker_used_4
  *set num_stickers_used + 1
*if sticker_used_5
  *set num_stickers_used + 1
*return

*label func_print_time
*params print_ampm
*temp hours round(current_time / 100)
*if hours >= 13
  *set hours - 12
*temp minutes current_time modulo 100
*if minutes = 0
  *set minutes "00"
*temp ampm "AM"
*if current_time >= 1200
  *set ampm "PM"
${hours}:${minutes}
*if print_ampm
  ${ampm}
*return

*label func_print_time_readout
[b][i][ The time is
*gosub func_print_time true
][/i][/b]
*return